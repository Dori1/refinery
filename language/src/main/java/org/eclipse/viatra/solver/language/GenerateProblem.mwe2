module org.eclipse.viatra.solver.language.GenerateProblem

import org.eclipse.xtext.xtext.generator.*
import org.eclipse.xtext.xtext.generator.model.project.*

import org.eclipse.viatra.solver.language.mwe2.*

var rootPath = ".."

Workflow {
	component = XtextGenerator {
		configuration = {
			project = StandardProjectConfig {
				baseName = "language"
				rootPath = rootPath
				runtimeTest = {
					enabled = true
				}
				genericIde = {
					name = "language-ide"
				}
				web = {
					enabled = true
					name = "language-web"
					assets = "../language-web/build/generated/sources/xtext"
				}
				mavenLayout = true
			}
			code = {
				encoding = "UTF-8"
				lineDelimiter = "\n"
				fileHeader = "/*\n * generated by Xtext \${version}\n */"
				preferXtendStubs = false
			}
		}

		language = StandardLanguage {
			name = "org.eclipse.viatra.solver.language.Problem"
			fileExtensions = "problem"
			referencedResource = "platform:/resource/refinery-language-model/model/problem.genmodel"
			serializer = {
				generateStub = false
			}
			validator = {
				generateDeprecationValidation = true
			}
			generator = {
				generateStub = false
			}
			junitSupport = {
				junitVersion = "5"
			}
			webSupport = ProblemWebIntegrationFragment {
				highlightingPath = "js/mode-problem.js"
				generateHtmlExample = false
				generateJettyLauncher = false
				generateServlet = false
				generateWebXml = false
			}
		}
	}
}
