module tools.refinery.language.GenerateProblem

import org.eclipse.xtext.xtext.generator.*
import org.eclipse.xtext.xtext.generator.model.project.*

var rootPath = '..'

Workflow {
	component = XtextGenerator {
		configuration = {
			project = StandardProjectConfig {
				baseName = 'language'
				rootPath = rootPath
				runtimeTest = {
					enabled = true
					srcGen = 'src/testFixtures/xtext-gen'
				}
				genericIde = {
					name = 'language-ide'
				}
				web = {
					enabled = true
					name = 'language-web'
				}
				mavenLayout = true
			}
			code = {
				encoding = 'UTF-8'
				lineDelimiter = '\n'
				fileHeader = '/*\n * generated by Xtext \${version}\n */'
				preferXtendStubs = false
			}
		}

		language = StandardLanguage {
			name = 'tools.refinery.language.Problem'
			fileExtensions = 'problem'
			referencedResource = 'platform:/resource/refinery-language-model/model/problem.genmodel'
			serializer = {
				generateStub = false
			}
			formatter = {
				generateStub = true
			}
			validator = {
				generateDeprecationValidation = true
			}
			generator = {
				generateStub = false
			}
			junitSupport = {
				generateStub = false
				skipXbaseTestingPackage = true
				junitVersion = '5'
			}
			webSupport = {
				// We only generate the {@code AbstractProblemWebModule},
				// because we write our own integration code for CodeMirror 6. 
				framework = 'codemirror'
				generateHtmlExample = false
				generateJettyLauncher = false
				generateJsHighlighting = false
				generateServlet = false
				generateWebXml = false
			}
		}
	}
}
