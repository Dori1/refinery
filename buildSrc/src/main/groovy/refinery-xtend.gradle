plugins {
	id 'org.xtext.xtend'
	id 'refinery-java-conventions'
	id 'refinery-sonarqube'
}

sourceSets {
	main {
		xtendOutputDir = 'src/main/xtend-gen'
	}
	test {
		xtendOutputDir = 'src/test/xtend-gen'
	}
}

xtend.generator {
	// As of Xtext 2.26.0.M2, the Xbase compiler doesn't know how to handle Java 12 and up
	// and the build fails with a NullPointerException if such Java version is specified.
	javaSourceLevel = '11'
}

tasks.named('clean') {
	delete 'src/main/xtend-gen'
	delete 'src/test/xtend-gen'
}

sonarqube.properties {
	properties['sonar.exclusions'] += [
		'src/main/xtend-gen/**',
		'src/test/xtend-gen/**',
	]
}

tasks.named('generateEclipseSourceFolders') {
	dependsOn tasks.named('generateXtext')
	dependsOn tasks.named('generateTestXtext')
}
